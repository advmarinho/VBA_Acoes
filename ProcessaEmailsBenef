Sub SalvarEmailsBeneficios()
    Dim objItem As Object, objMail As MailItem
    Dim xlApp As Object, wb As Object, ws As Object
    Dim excelFilePath As String, lastRow As Long
    Dim senderEmail As String, senderName As String, senderDisplayName As String
    Dim emailDate As String, subject As String
    Dim classificationReason As String, suggestedMatricula As String

    ' Caminho do Excel
    excelFilePath = "C:\_RPA\Benef\Benef.xlsx"

    If Application.ActiveExplorer.Selection.Count = 0 Then
        MsgBox "Selecione ao menos um e-mail.", vbExclamation
        Exit Sub
    End If

    ' Abrir Excel
    On Error Resume Next
    Set xlApp = CreateObject("Excel.Application")
    On Error GoTo 0
    If xlApp Is Nothing Then
        MsgBox "Erro ao abrir o Excel. Verifique se ele não está travado.", vbCritical
        Exit Sub
    End If

    xlApp.Visible = False
    Set wb = xlApp.Workbooks.Open(excelFilePath)
    Set ws = wb.Sheets(1)

    ' Loop nos e-mails
    For Each objItem In Application.ActiveExplorer.Selection
        If TypeOf objItem Is MailItem Then
            Set objMail = objItem

            senderName = objMail.senderName
            'senderEmail = objMail.SenderEmailAddress
            senderEmail = GetSmtpAddress(objMail)
            senderDisplayName = ""

            If InStr(senderEmail, "/O=EXCHANGELABS") > 0 Then
                senderDisplayName = senderName
            End If

            emailDate = Format(objMail.SentOn, "yyyy_mm_dd")
            subject = IIf(Trim(objMail.subject) = "", "Sem_Assunto", objMail.subject)

            suggestedMatricula = FindMatricula(subject)
            If suggestedMatricula = "" Then suggestedMatricula = FindMatricula(objMail.Body)

            classificationReason = InputBox("Digite a matrícula ou motivo (Ex: Folha de Pagamento):", _
                                            "Classificação do E-mail", suggestedMatricula)
            If Trim(classificationReason) = "" Then
                MsgBox "Classificação obrigatória. E-mail ignorado."
                GoTo Proximo
            End If

            ' Gravar na planilha
            lastRow = ws.Cells(ws.Rows.Count, "A").End(-4162).Row + 1
            With ws
                .Cells(lastRow, 1).Value = subject
                .Cells(lastRow, 2).Value = senderEmail
                .Cells(lastRow, 3).Value = senderName
                .Cells(lastRow, 4).Value = emailDate
                .Cells(lastRow, 5).Value = "Atendimento"
                .Cells(lastRow, 6).Value = GetTheme(GetShortSubject(subject))
                .Cells(lastRow, 7).Value = classificationReason
                .Cells(lastRow, 8).Value = senderDisplayName ' nova coluna
                If InStr(classificationReason, "Folha de Pagamento") > 0 Then
                    .Cells(lastRow, 7).Interior.Color = RGB(173, 216, 230)
                End If
            End With

            ' Mover para lixeira (sem salvar email ou anexo)
            objMail.Move Application.Session.GetDefaultFolder(olFolderDeletedItems)
        End If
Proximo:
    Next objItem

    wb.Save
    wb.Close False
    xlApp.Quit

    Set xlApp = Nothing: Set wb = Nothing: Set ws = Nothing

    MsgBox "E-mails processados e movidos para a lixeira.", vbInformation
End Sub

Function FindMatricula(text As String) As String
    Dim regex As Object
    Set regex = CreateObject("VBScript.RegExp")
    
    regex.Pattern = "\b\d{4,5}\b"
    regex.Global = True
    regex.IgnoreCase = True

    If regex.Test(text) Then
        FindMatricula = regex.Execute(text)(0)
    Else
        FindMatricula = ""
    End If
End Function

Function GetShortSubject(subject As String) As String
    subject = Replace(subject, "RE:", "", , , vbTextCompare)
    subject = Replace(subject, "FW:", "", , , vbTextCompare)
    GetShortSubject = Trim(Left(subject, 40))
End Function

Function GetTheme(shortSubject As String) As String
    If InStr(1, shortSubject, "benefício", vbTextCompare) > 0 Then
        GetTheme = "Benefícios"
    ElseIf InStr(1, shortSubject, "rescisão", vbTextCompare) > 0 Then
        GetTheme = "Rescisão"
    Else
        GetTheme = "Outros"
    End If
End Function

Function GetSmtpAddress(mail As MailItem) As String
    Dim PR_SMTP_ADDRESS As String
    PR_SMTP_ADDRESS = "http://schemas.microsoft.com/mapi/proptag/0x39FE001E"
    
    On Error Resume Next
    If mail.SenderEmailType = "EX" Then
        ' Exchange interno – tenta resolver o endereço SMTP real
        GetSmtpAddress = mail.Sender.PropertyAccessor.GetProperty(PR_SMTP_ADDRESS)
    Else
        ' SMTP comum (externo)
        GetSmtpAddress = mail.SenderEmailAddress
    End If
    On Error GoTo 0
End Function



